#version 430 core

layout( local_size_x = 32 ) in; 
 
uniform float Gravity1 = 200.0; 
uniform vec3 BlackHolePos1 = vec3(-5.0, 0.0, 0.0); 
uniform float Gravity2 = 200.0; 
uniform vec3 BlackHolePos2 = vec3(5.0, 0.0, 0.0); 
 
uniform float ParticleInvMass = 1.0 / 4.0; 
uniform float dt; 
 
layout(std430, binding=0) buffer Pos { 
  vec4 Position[]; 
}; 
layout(std430, binding=1) buffer Vel { 
  vec4 Velocity[]; 
}; 
 
void main() { 
  uint idx = gl_GlobalInvocationID.x; 
 
  vec3 p = Position[idx].xyz; 
  vec3 v = Velocity[idx].xyz; 

  float h = 0.1 * dt;
 
  // Force from black hole #1 
  vec3 d = BlackHolePos1 - p; 
  vec3 force = (Gravity1 / length(d)) * normalize(d); 
   
  // Force from black hole #2 
  d = BlackHolePos2 - p; 
  force += (Gravity2 / length(d)) * normalize(d); 
 
  // Apply simple Euler integrator 
  vec3 a = force * ParticleInvMass; 
  Position[idx] = vec4( 
        p + v * h + 0.5 * a * h * h, 1.0); 
  Velocity[idx] = vec4( v + a * h, 0.0); 
} 
